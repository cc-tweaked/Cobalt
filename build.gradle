plugins {
    id "com.github.hierynomus.license" version "0.15.0"
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'maven'

group = 'org.squiddev'
version = '0.5.0-SNAPSHOT'

targetCompatibility = sourceCompatibility = 1.8
compileTestJava.sourceCompatibility = compileTestJava.targetCompatibility = 1.8

repositories {
	mavenCentral()
}

configurations {
	deployerJars
}

dependencies {
	testCompile 'org.luaj:luaj-jse:2.+'
	testCompile 'junit:junit:4.+'
	testCompile 'org.hamcrest:hamcrest-library:1.3'

	testCompile 'org.openjdk.jmh:jmh-core:1.21'
	testCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.21'

	deployerJars "org.apache.maven.wagon:wagon-ssh:3.3.1"
}

license {
	strictCheck true
	include("*.java")
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

uploadArchives {
	repositories {
		if(project.hasProperty('mavenUploadUrl')) {
			mavenDeployer {
				configuration = configurations.deployerJars

				repository(url: project.property('mavenUploadUrl')) {
					authentication(
						userName: project.property('mavenUploadUser'),
						privateKey: project.property('mavenUploadKey'))
				}

				pom.project {
					name 'Cobalt'
					packaging 'jar'
					description 'A reentrant fork of LuaJ for Lua 5.1'
					url 'https://github.com/SquidDev/Cobalt'

					scm {
						url 'https://github.com/SquidDev/Cobalt.git'
					}

					issueManagement {
						system 'github'
						url 'https://github.com/SquidDev/Cobalt/issues'
					}

					licenses {
						license {
							name 'MIT'
							url 'https://github.com/SquidDev/Cobalt/blob/master/LICENSE'
							distribution 'repo'
						}
					}
				}
			}
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			artifact sourcesJar { classifier "sources" }
			artifact javadocJar { classifier "javadoc" }
		}
	}
}

gradle.projectsEvaluated {
	tasks.withType(JavaCompile) {
		options.compilerArgs << "-Xlint"
	}
}

task executeProfile(type: JavaExec) {
	main = "org.squiddev.cobalt.PerformanceRunner"
	classpath = sourceSets.test.runtimeClasspath
}
